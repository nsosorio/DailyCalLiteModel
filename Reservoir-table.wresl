define S_Orovllevel1 {value 29.6}
define S_Orovl_1 {std kind 'STORAGE-ZONE' units 'TAF'}
goal S_OrovlZone1 {S_Orovl_1 < S_Orovllevel1}
define S_Orovllevel2 {value 852.}
define S_Orovl_2 {std kind 'STORAGE-ZONE' units 'TAF'}
goal S_OrovlZone2 {S_Orovl_2 < S_Orovllevel2-S_Orovllevel1}
define S_Orovl_3 {std kind 'STORAGE-ZONE' units 'TAF'}
define S_Orovl_4 {std kind 'STORAGE-ZONE' units 'TAF'}
define S_Orovllevel5 {timeseries kind 'STORAGE-LEVEL' units 'TAF'}
define S_Orovl_5 {std kind 'STORAGE-ZONE' units 'TAF'}
define S_Orovllevel6 {value 3558.}
define S_Orovl_6 {std kind 'STORAGE-ZONE' units 'TAF'}
define S_Orovl {std kind 'STORAGE' units 'TAF'} !OROVILLE LAKE AKA RES_NUM = 6
goal storage_Orovl {S_Orovl=S_Orovl_1+S_Orovl_2+S_Orovl_3+S_Orovl_4+S_Orovl_5+S_Orovl_6}
define F_Orovl {std kind 'FLOW-SPILL-NON-RECOV' units 'CFS'}
define E_Orovl {lower unbounded kind 'EVAPORATION' units 'CFS'}
define A_Orovl {std kind 'SURFACE-AREA' units 'ACRES'}
define evap_S_Orovl {timeseries kind 'EVAPORATION-RATE' units 'IN'}
define A_Orovllast {select area from res_info given storage=1000*S_Orovl(-1) use linear where res_num=6}
define A_Orovlforward {select area from res_info given storage=1050*S_Orovl(-1) use linear where res_num=6}
define A_Orovlback {select area from res_info given storage=950*S_Orovl(-1) use linear where res_num=6}
define coefev_Orovl {value (A_Orovlforward - A_Orovlback)/(100*max(0.01,S_Orovl(-1)))}
goal area_Orovl {A_Orovl=A_Orovllast+1000*coefev_Orovl*S_Orovl-1000*coefev_Orovl*S_Orovl(-1)}
goal evap_Orovl {E_Orovl*cfs_af=(evap_S_Orovl/24)*A_Orovllast+(evap_S_Orovl/24)*A_Orovl}
define relcap_Orovl {select discharge from res_info given storage=1000*S_Orovl(-1) use linear where res_num=6}
goal maxrelease_Orovl {C_Orovl<relcap_Orovl}
