/*************************************
CVPcut_system.wresl
 
Tom FitzHugh BOR 5/7/2010

This file calculates delivery cutbacks based on total CVP system demands.  Percent 
deliveries calculated here are used to cutback individual NOD deliveries.  They are 
also used in the SOD if the percent deliveries calculated here are less than the 
percent deliveries computed in CVPcut_south.wresl.

Tiers 5 and 6 are currently not implemented, but left commented code in here to
do that if need be.

1/7/2011: modified code to incorporate user-defined SOD CVP demands into cutback procedure -TF
*************************************/
! deltar_cvp_sys    Forecast CVP delivery - Input from CVP delivery logic
! need to subtract any reduction in SOD demands from user-define demands from demand amount
define cut_delivery_sys {value  max(min(acvpdem_dr_sys, acvpdem_tot_sys) - acvpdem_tot_s_red - deltar_cvp_sys, 0.0) }  !Total required cut in deliveries

! Delivery Reduction Percent by Tier
define tier0cut_sys {

       case Critical {
            condition wyt_SHASTA_CVP == 4
            value     0.25}
       case rest {
            condition always
            value     0.00}
       }

define tier0excut_sys {

       case Critical {
            condition wyt_SHASTA_CVP == 4
            value     0.23}                   !Exchange goes to 77% not 75%
       case rest {
            condition always
            value     0.00}
       }
              
define tier1cut_sys {value 0.25} !1st Tier Reduced 1st
define tier2cut_sys {value 0.25} !2nd Tier Reduced Next
define tier3cut_sys {value 0.25} !3rd Tier Reduced Next
define tier4cut_sys {value 0.25} !4th Tier Reduced Next

!************ Tier 0 **************
!*  takes SC, EX, RF down to 75%  *         No intermediate steps here.  If the ShastaIndex is critical, then
!*  when ShastaIndex Critical     *         Settlement Contracts and Exchange Contracts and Refuge Contracts are cut from 100% to 75%.
!**********************************
define cut0_sys     { value tier0cut_sys*acvpdem_psc_sys + tier0excut_sys*acvpdem_pex_sys + tier0cut_sys*acvpdem_prf_sys*CVP_prf_ratio_sys }


!*********** Tier 1 ************
!*    takes AG down to 75%     *
!*******************************
define cut1_sys     { value max(0.0, cut_delivery_sys - cut0_sys) }
define percut1_sys  { value min(tier1cut_sys, cut1_sys/(acvpdem_pag_sys*CVP_pag_ratio_sys)) }
define cut_pag1_sys { value percut1_sys*acvpdem_pag_sys*CVP_pag_ratio_sys }

!*********** Tier 2 ************
!*  takes AG to 50%; MI to 75% *
!*******************************
define cut2_sys     { value max(0.0, cut1_sys - cut_pag1_sys) }
define percut2_sys  { value min(tier2cut_sys, (cut2_sys/(acvpdem_pag_sys*CVP_pag_ratio_sys + acvpdem_pmi_sys*CVP_pmi_ratio_sys))) }
define cut_pag2_sys { value percut2_sys*acvpdem_pag_sys*CVP_pag_ratio_sys }
define cut_pmi2_sys { value percut2_sys*acvpdem_pmi_sys*CVP_pmi_ratio_sys }

!*********** Tier 3 ***********
!  takes AG to 25%            *
!******************************
define cut3_sys     { value max(0.0, cut2_sys - cut_pag2_sys - cut_pmi2_sys) }
define percut3_sys  { value min(tier3cut_sys, (cut3_sys/(acvpdem_pag_sys*CVP_pag_ratio_sys))) }
define cut_pag3_sys { value percut3_sys*acvpdem_pag_sys*CVP_pag_ratio_sys }

!*********** Tier 2 ************
!*  takes AG to 50%; MI to 75% *
!*******************************
define cut2_sys     { value max(0.0, cut1_sys - cut_pag1_sys) }
define percut2_sys  { value min(tier2cut_sys, (cut2_sys/(acvpdem_pag_sys*CVP_pag_ratio_sys + acvpdem_pmi_sys*CVP_pmi_ratio_sys))) }
define cut_pmi2_sys { value percut2_sys*acvpdem_pmi_sys*CVP_pmi_ratio_sys }

!*********** Tier 4 *********
!  takes AG to 0; MI to 50% *
!****************************
define cut4_sys     { value max(0.0, cut3_sys - cut_pag3_sys) }
define percut4_sys  { value min(tier4cut_sys, (cut4_sys/(acvpdem_pag_sys*CVP_pag_ratio_sys + acvpdem_pmi_sys*CVP_pmi_ratio_sys))) }
define cut_pmi4_sys { value percut4_sys*acvpdem_pmi_sys*CVP_pmi_ratio_sys }

!**********  Totals  ********
define cut_pmi_sys { value cut_pmi2_sys + cut_pmi4_sys /*+ cut_pmi5_sys*/}

!********** Allocation Percentages *********
define alc_mi_sys { alias 1 - percut2_sys - percut4_sys /*- percut5_sys*/ kind 'percent-allocation' units 'none'}

!********** Percent Delivery (Should equal the allocation) ****************
! Calculate percent of full demand to deliver
define perdel_cvpmi_sys { value max(0.0, 1 - cut_pmi_sys/(acvpdem_pmi_sys*CVP_pmi_ratio_sys)) }   !Percent of full delivery for MI

! Final Percent Delivery for output
define perdv_cvpmi_sys { alias  perdel_cvpmi_sys kind 'percent-delivery' units 'none'}
